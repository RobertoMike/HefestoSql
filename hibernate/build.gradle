plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'io.github.robertomike'
version = '2.0.0'

repositories {
    mavenCentral()
}

ext { // this are variables
    pomGroupId = group
    pomVersion = version
    artifactId = 'hefesto-hibernate-base'

    jdkCompileVersion = 17
}

compileJava.options.encoding = 'UTF-8'

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

dependencies {
    implementation 'org.hibernate:hibernate-core:6.0.0.Final'
    api 'org.hibernate:hibernate-core:6.0.0.Final'

    implementation project(path: ':hefesto-base')
    api project(path: ':hefesto-base')

    compileOnly("com.google.auto.service:auto-service:1.0.1")
    annotationProcessor("com.google.auto.service:auto-service:1.0.1")

    // https://projectlombok.org
    implementation 'org.projectlombok:lombok:1.18.30'
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    testCompileOnly 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'

    testImplementation 'mysql:mysql-connector-java:8.0.28'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.10.0"
    testImplementation 'org.mockito:mockito-core:5.4.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.4.0'
    testImplementation 'org.mockito:mockito-inline:5.2.0'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.9'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        library(MavenPublication) {
            from components.java

            groupId = "${pomGroupId}"
            artifactId = "${artifactId}"
            version = "${pomVersion}"

            pom {
                name = 'HefestoSql'
                description = 'HefestoSql is an open-source Java library for creation of query with hibernate'
                url = 'https://github.com/RobertoMike/HefestoSql'
                inceptionYear = '2024'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        name = 'Roberto Micheletti'
                        email = 'rmworking@hotmail.com'
                        organization = 'Roberto Micheletti'
                        organizationUrl = 'https://github.com/RobertoMike'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/RobertoMike/HefestoSql.git'
                    developerConnection = 'scm:git:ssh://github.com:RobertoMike/HefestoSql.git'
                    url = 'https://github.com/RobertoMike/HefestoSql'
                }
            }
        }
    }
    repositories {
        maven {
            name = "central_repository_ossrh"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("OSSRH_USERNAME")
                password = System.getenv("OSSRH_PASSWORD")
            }
            metadataSources {
                gradleMetadata()
            }
        }
    }
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compileClasspath
}

signing {
    setRequired { !version.toString().endsWith("SNAPSHOT") }
    sign publishing.publications.library
}

tasks.register('printVersion') {
    doLast {
        println project.version
    }
}

java {
    withJavadocJar()
    withSourcesJar()
    toolchain {
        languageVersion.set(JavaLanguageVersion.of("${jdkCompileVersion}"))
    }
}